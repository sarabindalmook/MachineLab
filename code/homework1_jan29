#include <Servo.h> 

const int potPin = A0; //UPDATED: potentiometer connected to A0

class Flasher
{

	int ledPin;    
	long OnTime;
	long OffTime;

	int ledState;    
	unsigned long previousMillis;  

  public:
  Flasher(int pin, long on, long off)
  {
	ledPin = pin;
	pinMode(ledPin, OUTPUT);     
	  
	OnTime = on;
	OffTime = off;
	
	ledState = LOW; 
	previousMillis = 0;
  }

  void Update()
  {
    unsigned long currentMillis = millis();
     
    if((ledState == HIGH) && (currentMillis - previousMillis >= OnTime))
    {
    	ledState = LOW; 
      previousMillis = currentMillis;  
      digitalWrite(ledPin, ledState);
    }
    else if ((ledState == LOW) && (currentMillis - previousMillis >= OffTime))
    {
      ledState = HIGH;  
      previousMillis = currentMillis;  
      digitalWrite(ledPin, ledState);	 
    }
  }
};

class Sweeper
{
  Servo servo;   
  int pos;             
  int increment;       
  int  updateInterval;      
  unsigned long lastUpdate; 

public: 
  Sweeper(int interval)
  {
    updateInterval = interval;
    increment = 1;
    pos = 0; //UPDATED: initialize so servo doesn't start "random"
    lastUpdate = 0; //UPDATED: initialize timing
  }
  
  void Attach(int pin)
  {
    servo.attach(pin);
  }
  
  void Detach()
  {
    servo.detach();
  }
  
  void SetInterval(int intervalMs) //UPDATED: allow changing sweep speed
  {
    updateInterval = intervalMs;
  }

  void Update()
  {
    if((millis() - lastUpdate) > updateInterval)
    {
      lastUpdate = millis();
      pos += increment;
      servo.write(pos);
      Serial.println(pos);
      if ((pos >= 180) || (pos <= 0))
      {
        increment = -increment;
      }
    }
  }
};
 
 
Flasher led1(11, 123, 400);
Flasher led2(12, 350, 350);
Flasher led3(13, 200, 222);

Sweeper sweeper1(15);
Sweeper sweeper2(25);
 
void setup() 
{ 
  Serial.begin(9600);
  pinMode(2, INPUT_PULLUP);
  sweeper1.Attach(6);
  sweeper2.Attach(10);
} 
 
 
void loop() 
{ 
  int potVal = analogRead(potPin); //UPDATED: read potentiometer (0â€“1023)
  int interval = map(potVal, 0, 1023, 2, 200); //UPDATED: smaller = faster, bigger = slower
  sweeper1.SetInterval(interval); //UPDATED: apply speed to sweeper 1
  sweeper2.SetInterval(interval); //UPDATED: apply speed to sweeper 2
  
  if(digitalRead(2) == LOW)
  {
     sweeper1.Update();
     sweeper2.Update();
     led1.Update();
  }
  
  led2.Update();
  led3.Update();
}
